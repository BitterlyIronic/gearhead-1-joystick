Story
	name <STORY: Pump Pushers>

	% This story will run for ~3 days, or until completed by the PC.
	% To complete, the PC must go on a relay from town to town
	% completing plots at each stop.

	% Drug smugglers are operating in the FedTer.
	% The PC will have to investigate their whereabouts, eventually leading a
	% sting on their manufacturing plant.

	% Element 1 -> Controlling Faction
	% Element 2 is the scene where the relay will take place.
	%  The value of N2 will be changed by plots over the course
	%  of this adventure.
	Element2 <Scene Town !Member 11>

	% **********************
	% ***  SCRIPT BLOCK  ***
	% **********************
	%  S1 = Time Limit
	%  S2 = Challenge Rating
	%  S3 = Message Randomizer

	%  S101 = Faction Victory Count
	%      Add one for every mission the PC won,
	%      even if it wasn't a relay mission. The relays
	%      should probably be worth more, though.
	%  S103 = How many times PC fought for faction
	%  S104 = If nonzero, PC has won the final mission
	%         Note that the winning plot should call ENDSTORY
	%         by itself. This var is just to communicate to
	%         the investigation story that the PC should be
	%         rewarded.

	%  S201 = Load new "relay" plot if S201=0
	%  S202 = Relay victory count; load conclusion when S202>3
	%  S203 = Number of clues accumulated
	%  S204 = Load new "investigator" plot when S204 = 0
	%  S205 = Time limit for investigation; new clue
	%         acquired when time limit is past.

	% At the START trigger, check for the end of the story
	start <if= V1 0 else GoCheckTime V= 1 ComTime V+ 1 172800 V+ 1 d172800 V= 2 PCRep 6>
	GoCheckTime <ifG ComTime V1 else GoCheckMemo EndStory>

	% If the PC has taken part in the relay, store a memo.
	GoCheckMemo <if= S203 S202 ifG S103 0  Memo 1>

	% Every half-hour, load a new plot.
	halfhour <if# S2 0 if= S201 0 else GoLoadInv ifG S202 3 else GoLoadRelay StoryLine .endplot else GoLoadFixer S= 201 1 S+ 2 d4>
	GoLoadRelay <StoryLine .relayplot else GoLoadFixer FacXP+ N1 1 S= 201 1 S+ 2 d4>
	GoLoadInv <if= S204 0 else GoLoadGen StoryLine .invplot S= 204 1>
	GoLoadGen <if= d2 1 StoryLine .genplot>
	GoLoadFixer <StoryLine .fixplot>
	% Fill in the filename patterns for the plot types.
	.endplot    <TS_GRD_PumpPusher_PLOT2_*.txt>
	.relayplot  <TS_GRD_PumpPusher_PLOT1_*.txt>
	.invplot   <TS_GRD_PumpPusher_PLOT0_*.txt>
	.genplot <TS_GRD_PumpPusher_PLOTX_*.txt>
	.fixplot   <TS_GRD_PumpPusher_PLOTF_*.txt>

	% Every quarter, load a news story.
	% Implementation thereof is left as an exercise to the reader.
	quarter <News 101>

	% ***********************
	% ***  MESSAGE BLOCK  ***
	% ***********************
	% 1 :  Relay Memo
	Msg1 <The clues in the Pump Pusher case have led to \SCENE N2 .>

	% 101 - 105 :  News Messages
	Msg101 <Deadly new drug "Pump" is latest craze among lowlifes of FedTer. Citizens Morality Club to hold rally.>
	Msg101_1 <Guardians investigate "Pump Pushers": smuggling ring imports illegal narcotics into Federated Territories.>
	Msg101_2 <Several deaths at \NARRATIVE 2 night club attributed to use of the drug "Pump".>
	Msg101_3 <Activists call for legalization of Pump, Nicotine, and other banned substances in \NARRATIVE 2 today.>
	Msg101_4 <Guardians take tough stand against drug pushers after several deaths linked to use of "Pump".>
	Msg101_5 <Scientific evidence inconclusive; long term risks of street drug "Pump" not currently known.>
	Msg101_6 <Shootout in \NARRATIVE 2 though to be linked to "Pump Pushing". Guardians crack down on drug offenses.>


	% ***********************
	% ***  LINKING PLOTS  ***
	% ***********************

	% If a relay plot ends in victory for N1, do the following:
	%  S= 201 0 S+ 202 1 S+ 101 d3 N= 2 [element number of next scene]
	% If it ends in failure, or gets cancelled:
	%  S= 201 0

