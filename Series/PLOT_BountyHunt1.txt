Plot 0
	name <PLOT: Bounty Hunt 1>

    % v1.303 Standard

	% Element1 is just the leader of some town.
	element1 <Character Bounty Hunter>

	% ********************
	% *** SCRIPT BLOCK ***
	% ********************
	%  P1 = Time Limit
	%  P2 = Combat Indicator, if V2=1 combat has been entered
	%  P3 = Email sent indicator.

	% Time limit. This plot will conclude in one day, or immediately
	% if NPC E1 is killed.
	start <ifNPCOK E1 else GoDelete if= V1 0 else GoCheckTime V= 1 ComTime V+ 1 86400>
	GoCheckTime <if= V2 0 ifG ComTime V1 AdvancePlot 0>
	GoDelete <AdvancePlot 0>

	% Combat check. If combat has been entered, V2=1.
	% Combat ends in victory if the number of active masters on
	% team two drops to zero, or ends in defeat is the number of
	% active masters on team one drops to zero. After combat, V2=3
	% if the player was victorious or V2=2 is the player lost.
	nu1 <if= T1 0 if= V2 1  V= 2 2  LoseRenown>
	nu2 <if= T2 0 if= V2 1  V= 2 3  Reputation 6  1  XPV 50>

	% Email check. The NPC might send the PC email about this mission.
	5Min <if= d80 23 if= V3 0 ifG RReact E1 d150 ifPCLawful V= 3 1 Email 1>

	% *********************
	% *** MESSAGE BLOCK ***
	% *********************

	% 01 - 05 :  Email Messages.
	%  The NPC is offering the PC a mission via email.
	Msg1   <\PERSONA E1 @ \SCENE EScene 1 :// I just posted a bounty on some criminals. Come see me if you want the job.>
	Msg1_2 <\PERSONA E1 @ \SCENE EScene 1 :// Just posted a bounty notice; check it out if you're in the area.>
	Msg1_3 <\PERSONA E1 @ \SCENE EScene 1 :// We have some dangerous criminals over this way; I need someone to bring them in.>
	Msg1_4 <\PERSONA E1 @ \SCENE EScene 1 :// Looking for work? I may have something.>
	Msg1_5 <\PERSONA E1 @ \SCENE EScene 1 :// I need a hunter to bring in some criminals. Call me if you're interested.>

sub
	% This next bit is the conversation which will happen when the
	% PC speaks to E1.
	Persona 1
	rumor <!1 just posted a bounty notice for a wanted criminal.>

	% ********************
	% *** SCRIPT BLOCK ***
	% ********************
	%  V1 = Job offer counter, V1=1 is job offered, V1=-1 if job denied
	%  V3 = Threat value of enemy mecha
	%  V4 = Reward offered to PC for the mission

	greeting <NewChat if= P2 3 else GoCheckLoss Say 1 AddReact d5 CashPrize V4 AdvancePlot 0>
	GoCheckLoss <if= P2 2 else GoCheckMission Say 6 AdvancePlot 0>
	GoCheckMission <if= V1 1 else GoCheckFirst Say 101 Goto GoAddComOps>

	% The first time the PC speaks to the NPC, initialize everything.
	GoCheckFirst <if= V1 0 else GoDenied V= 1 -1 V= 3 PCRep 6 V= 4 Reward Threat V3 100 250 Goto GoCheckOffer>
	GoCheckOffer <if= P3 1 else GoMakeOffer Say 11  Goto GoAddAcceptOps>
	% If the PC has "the right stuff", an offer will be made immediately.
	% If not, no offer will be made at all. In the example provided
	% below, "the right stuff" is a Lawful reputation + being liked.
	GoMakeOffer  <ifG React 50 else GoMediumOffer Say 16 Goto GoAddAcceptOps>
    GoMediumOffer <ifG React d30 else GoBadOffer ifG PCRenown d20 else GoBadOffer Say 17 Goto GoAddAcceptOps>
    GoBadOffer <Say 18 AddChat 2 AddChat 3>

	GoAddAcceptOps <AddChat 1 AddChat 4>
	GoAddComOps <AddChat 7 AddChat 11>
	% If the mission was denied but not previously cleaned up, do
	% that now.
	GoDenied <SayAnything AdvancePlot 0>

	% PC will accept the job. Descibe the mission.
	result1 <NewChat V= 1 1 Say 21 Memo 103 Goto GoAddComOps>

    % If the PC wasn't offered the job immediately, the pay gets cut.
    result2 <NewChat V= 4 Reward Threat V3 100 150 ifG SkRoll 19 7 else GoR2NoCon Say 3 Goto GoAddAcceptOps>
	GoR2NoCon <ifG PCRep 6 d35 else GoR2NoRep Say 4 Goto GoAddAcceptOps>
	GoR2NoRep <Say 5 AdvancePlot 0>
    result3 <EndChat Say 2 AdvancePlot 0>

	% PC refuses the job.
	result4 <NewChat Say 104 AdvancePlot 0>

	% PC will enter combat now.
	result7 <EndChat Say 26 Goto GoStartCombat>
	GoStartCombat <SavePos P= 2 1 MDynamic 0 V3 .mon .nu1 .nu2 SetMapType 7  Reputation 2 1>
	.nu1 <if= T1 0 Return>
	.nu2 <if= T2 0 Return>
    .mon <CRIMINAL>

	% PC needs some time to get ready.
	result11 <NewChat Say 105>

	% *********************
	% *** MESSAGE BLOCK ***
	% *********************

	% 01 :  Player has won the battle and is being paid.
	Msg1 <Good job. Here's your reward.>
	Msg1_2 <>
	Msg1_3 <>
	Msg1_4 <>
	Msg1_5 <>

    Msg2 <Good day to you.>
    Msg3 <There's almost always bounties available. I just received a contract on a vicious gang of criminals. You interested in being a hunter?>
    Msg4 <A new contract just arrived today. You look like you might have a bit of talent... Want to hear about the case?>
    Msg5 <No, nothing at the moment.>

	% 06 :  Player has lost the battle.
	Msg6 <Better luck next time.>
	Msg6_7 <>
	Msg6_8 <>
	Msg6_9 <>
	Msg6_10 <>

	% 11 :  PC got email about mission; asked to accept
	Msg11 <We've got a wanted criminal in this area. Your job would be to bring him and his gang in, dead or alive. Upon completion of the mission I'd present you with a load of money. Does that sound good?>
	Msg11_2 <>
	Msg11_3 <>
	Msg11_4 <>
	Msg11_5 <>

	% 16 :  Player has "the right stuff"; will be offered mission.
	Msg16 <Hey there, hotshot. How would you be interested in doing some bounty hunting? I've got a contract on a wanted criminal, if you're up for it.>
	Msg16_7 <I just got word of a big contract on an escaped criminal. If you were interested in some bounty hunting work, I could let you be the first one on the case.>
	Msg16_8 <>
	Msg16_9 <>
	Msg16_0 <>

    % 17 :  Player will be offered mission, but unenthusiastically.
    Msg17 <I'm looking for someone to do some hunting work. There's an escaped criminal with a huge bounty on his head that just moved into town. Are you interested?>

    % 18 :  Player can nose around for a job.
	Msg18 <Can I help you? I'm pretty busy right now.>
	Msg18_1 <What do you want? I'm busy.>
	Msg18_2 <Why are you bothering me?>
    CMsg18_2 <ifG d20 react Accept>

	% 21 :  PC has accepted; describe the mission.
	Msg21 <The target is a violent crime gang. Your salary will be $ \VAL V4 .>
	Msg21_1 <Your target is a group of deadly terrorists. Your fee for the mission will be $ \VAL V4 .>
    CMsg21_1 <ifG PCRenown 50 Accept>
	Msg21_2 <This bounty notice is for a deadly cult of thrill-killers. Pay will be $ \VAL V4 .>
    CMsg21_2 <ifG PCRenown 65 Accept>
	Msg21_3 <The target this time is a gang of hired killers. The reward for this job is $ \VAL V4 , payable dead or alive.>
	Msg21_4 <This bounty notice is for the organizers of an underground slave trading cartel. You'll earn $ \VAL V4 for this job.>
	Msg21_5 <Your target is a violent gang of smugglers. Your fee for the mission will be $ \VAL V4 .>
	Msg21_6 <This reward is for a violent gang of outlaw bootleggers. Pay will be $ \VAL V4 .>
	Msg21_7 <Your target is a minor crime lord and his cronies. Pay will be $ \VAL V4 .>
    CMsg21_7 <ifG PCRenown 25 Accept>
	Msg21_8 <There's a reward available for a renegade army unit. Pay will be $ \VAL V4 .>
    CMsg21_8 <ifG PCRenown 35 Accept>
	Msg21_9 <Your target is a particularly deadly gang of outlaws. You'll be up against overwhelming odds, but if anyone can survive this mission it's gonna be you. Your salary will be $ \VAL V4 .>
    CMsg21_9 <ifG PCRenown 75 Accept>
	Msg21_10 <Your target will be a ring of organ harvesters. Pay will be $ \VAL V4 .>
    CMsg21_10 <ifG PCRenown 20 Accept>
	Msg21_11 <This reward is for a drug gang. Pay will be $ \VAL V4 , payable dead or alive.>
	Msg21_12 <Your target is a high school murder club. Your salary will be $ \VAL V4 .>
	Msg21_13 <This time you'll be up against a band of religious vigilantes. Pay will be $ \VAL V4 .>
	Msg21_14 <There's a reward available for a group of hardened criminals. You'll earn $ \VAL V4  for this job.>
	Msg21_15 <This bounty notice is for a gang of hired thugs. Your fee for the mission will be $ \VAL V4 .>
	Msg21_16 <Your target is an up-and-coming crime gang. Pay will be $ \VAL V4 .>
	Msg21_17 <You'll be facing a small band of endorphin harvesters. Your salary will be $ \VAL V4 .>
	Msg21_18 <This bounty notice is for a team of mecha thieves. The reward for this job is $ \VAL V4 , payable dead or alive.>
	Msg21_19 <Your target is a gang of wannabe terrorists. Pay will be $ \VAL V4 .>
    CMsg21_19 <ifG 50 PCRenown Accept>
	Msg21_20 <This bounty notice is for a gang of vicious armed robbers. The reward for this job is $ \VAL V4 .>
    CMsg21_20 <ifG 40 PCRenown Accept>
	Msg21_21 <Your target is a band of outlaws. Pay will be $ \VAL V4 .>
	Msg21_22 <You'll be facing a den of thieves. The reward is $ \VAL V4  for the gang, payable dead or alive.>
	Msg21_23 <This bounty notice is for a gang of lowlife hoodlums. Pay will be $ \VAL V4 .>
    CMsg21_23 <ifG 20 PCRenown Accept>
	Msg21_24 <You'll be going up against a group of poachers. Your fee for the mission will be $ \VAL V4 .>
	Msg21_25 <There's a reward available for these escaped convicts. Your salary will be $ \VAL V4 .>
	Msg21_26 <Your target is a small-time street gang. The reward for this job is $ \VAL V4 , payable dead or alive.>
    CMsg21_26 <ifG 15 PCRenown Accept>
	Msg21_27 <You'll be facing a group of armed robbers on the run from the law. Your salary will be $ \VAL V4 .>
	Msg21_28 <Your target is a local protection racket. The reward for this job is $ \VAL V4 .>
	Msg21_29 <You'll be bringing in a group of robbers. Pay will be $ \VAL V4 .

	% 26 :  Combat is starting
	Msg26 <This is where you will find the targets. I don't care whether you have to bring them in dead or alive. Return them to me and I'll give you your reward.>
	Msg26_1 <>
	Msg26_7 <>
	Msg26_8 <>
	Msg26_9 <>
	Msg26_10 <>


	% 101 :  Player is back from repairs, time to start mission.
	Msg101 <So you're back. Are you ready for the mission we discussed earlier? Same details apply.>
	% 103 :  Memo
	Msg103 <\ELEMENT 1 in \SCENE EScene 1 gave you a bounty hunting mission.>
	% 104 :  PC didn't want the job.
	Msg104 <In that case, sorry for wasting your time.>
	Msg104_1 <That's really too bad. You've got raw skills, and you could make a lot of money in this business.>
    CMsg104_1 <ifG react 20 Accept>
	Msg104_2 <In that case, get out of here. I've got to find someone who can complete this contract.>
    CMsg104_2 <ifG d6 react Accept>
	% 105 :  PC needs time to get ready.
	Msg105 <Hurry back. Another hunter could very well catch the targets first.>

	% 01 :  PC will accept the mission.
	Prompt1 <Alright, tell me more.>
	Prompt1_2 <I think I'd like that.>
	Prompt1_3 <>

    % 02 - 03 :  Player will ask about mission, or not.
    Prompt2 <Do you have any bounties available?>
    Prompt3 <I'll come back later.>

	% 04 :  PC doesn't want this mission.
	Prompt4 <I'm not interested.>
	Prompt4_5 <Uh, I don't think I want this job.>
	Prompt4_6 <>

	% 07 :  Combat is starting.
	prompt7 <I'm ready to go.>
	prompt7_8 <>

	% 11 :  Just a minute...
	prompt11 <Wait... I need time to prepare.>
	prompt11_2 <>

end
