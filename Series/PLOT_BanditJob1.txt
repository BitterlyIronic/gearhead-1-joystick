Plot 0
	name <Bandit Job 1 Mk2>

    % Leader mission template
    % v1.302 Standard

	element1 <Character Bandit HasMecha>

	% ********************
	% *** SCRIPT BLOCK ***
	% ********************
	%  P1 = Time Limit
	%  P2 = Combat Indicator, if V2=1 combat has been entered
	%  P3 = Email sent indicator.

	% Time limit. This plot will conclude in one day, or immediately
	% if NPC E1 is killed.
	start <ifNPCOK E1 else GoDelete if= V1 0 else GoCheckTime V= 1 ComTime V+ 1 21600>
	GoCheckTime <if= V2 0 ifG ComTime V1 AdvancePlot 0>
	GoDelete <AdvancePlot 0>

	% Combat check. If combat has been entered, V2=1.
	% Combat ends in victory if the number of active masters on
	% team two drops to zero, or ends in defeat is the number of
	% active masters on team one drops to zero. After combat, V2=3
	% if the player was victorious or V2=2 is the player lost.
	nu1 <if= T1 0 if= V2 1  V= 2 2  LoseRenown>
	nu2 <if= T2 0 if= V2 1  V= 2 3  Salvage Reputation 6  1  XPV 50>

	% Email check. The NPC might send the PC email about this mission.
	5Min <if= d80 23 if= V3 0 ifG RReact E1 d100 ifPCChaotic V= 3 1 Email 1>

	% *********************
	% *** MESSAGE BLOCK ***
	% *********************

	% 01 - 05 :  Email Messages.
	%  The NPC is offering the PC a mission via email.
	Msg1   <\PERSONA E1 @ \SCENE EScene 1 :// I have a business proposition for you.>
	Msg1_2 <\PERSONA E1 @ \SCENE EScene 1 :// Looking for some quick cash? Call me.>
	Msg1_3 <\PERSONA E1 @ \SCENE EScene 1 :// You do dirty deeds at reasonable prices, right? I may need your services.>
	Msg1_4 <\PERSONA E1 @ \SCENE EScene 1 :// If you're in the area I could use a partner in crime.>
	Msg1_5 <\PERSONA E1 @ \SCENE EScene 1 :// Want to help me steal some crap? Give me a call.>

sub
	% This next bit is the conversation which will happen when the
	% PC speaks to E1.
	Persona 1

	rumor <!1 is looking for a partner in crime.>

	% ********************
	% *** SCRIPT BLOCK ***
	% ********************
	%  V1 = Job offer counter, V1=1 is job offered, V1=-1 if job denied
	%  V3 = Threat value of enemy mecha
	%  V4 = Reward offered to PC for the mission

	greeting <NewChat if= P2 3 else GoCheckLoss Say 1 AddReact d5 CashPrize V4 AdvancePlot 0>
	GoCheckLoss <if= P2 2 else GoCheckMission Say 6 AdvancePlot 0>
	GoCheckMission <if= V1 1 else GoCheckFirst Say 101 Goto GoAddComOps>

	% The first time the PC speaks to the NPC, initialize everything.
	GoCheckFirst <if= V1 0 else GoDenied V= 1 -1 V= 3 Threat PCRep 6 100 V= 4 Reward V3 150 Goto GoCheckOffer>
	GoCheckOffer <if= P3 1 else GoMakeOffer Say 11  Goto GoAddAcceptOps>
	GoMakeOffer  <ifG React d25 else GoNoOffer ifPCNotLawful else GoNoOffer ifG PCMeks 0 else GoNoMeks Say 16 Goto GoAddAcceptOps>

	GoAddAcceptOps <AddChat 1 AddChat 4>
	GoAddComOps <AddChat 7 AddChat 9 AddChat 11>

    GoNoMeks <Say 106 AdvancePlot 0>
	GoNoOffer <Say 102 AddChat 201 AddChat 202>
	% If the mission was denied but not previously cleaned up, do
	% that now.
	GoDenied <SayAnything AdvancePlot 0>

	% PC will accept the job. Descibe the mission.
	result1 <NewChat V= 1 1 Say 21 Memo 103 Goto GoAddComOps>

	% PC refuses the job.
	result4 <NewChat Say 104 AdvancePlot 0>

	% PC will enter combat now.
	result7 <EndChat Say 26 Goto GoStartCombat>
	GoStartCombat <SavePos P= 2 1 Dynamic 2 V3 .nu1 .nu2 Reputation 2 -2 Encounter 5 0 .enemydata>
	.nu1 <if= T1 0 Return>
	.nu2 <if= T2 0 Salvage Return>
	.enemydata <Chaotic ArchEnemy +PCRA>


	% PC will ask how many points the enemy has.
	result9 <NewChat Say 31 AddChat 7 AddChat 11>

	% PC needs some time to get ready.
	result11 <NewChat Say 105>

    result201 <ifG SkRoll 28 10 else GoNoInt NewChat Say 201 Goto GoAddAcceptOps>
	GoNoInt <EndChat Say 203 AddReact -d10 AdvancePlot 0>
    result202 <NewChat Say 202 AdvancePlot 0>

	% *********************
	% *** MESSAGE BLOCK ***
	% *********************

	% 01 :  Player has won the battle and is being paid.
	Msg1 <Here's your share of the take. You are the best, the absolute best.>
	Msg1_1 <Amazing. That mission came off so much better than it looked like it was going to. Here's your cash, as promised.>
	Msg1_2 <You really are worth your rates. Here's your money; I'll call you if I have another mission.>
	Msg1_3 <It's no wonder that you have such a slick rep. Here's your cash. I hope to do business with you again.>
	Msg1_4 <It's been a pleasure doing business with you. Here's the money.>
	Msg1_5 <You actually managed to beat them... I thought for sure you were gonna die. Well, here's the cash I owe you.>
	Msg1_6 <You could have a very bright future in this business. Here's the cash.>
	Msg1_7 <Stick with me and we'll both be rich. Here's your cut.>
	Msg1_8 <That's done. Here's the money I owe you.>
	Msg1_9 <Will you accept your pay in instant noodles? No? I didn't think so... here you are.>

	% 06 :  Player has lost the battle.
	Msg6_1 <That was a fiasco! We're lucky to have gotten out alive.>
	Msg6_2 <How many meks did they have!? That was not good...>
	Msg6_3 <I can't believe we lost. I don't want to talk about it.>
	Msg6_4 <I thought you were a big shot pilot!? We lost, big time.>
	Msg6_5 <That mission really, really sucked. Try not to do so badly ever again.>

	% 11 :  PC got email about mission; asked to accept
	Msg11 <Hey \PC , I see you got my mail. Want me to tell you about a great money-making opportunity?>
	Msg11_2 <>
	Msg11_3 <>
	Msg11_4 <>
	Msg11_5 <>

	% 16 :  Player has "the right stuff"; will be offered mission.
	Msg16 <Are you looking to make a bit of quick cash?>
	Msg16_7 <>
	Msg16_8 <>
	Msg16_9 <>
	Msg16_0 <>

	% 21 :  PC has accepted; describe the mission.
	Msg21 <A rival gang has been moving in on our territory. I need a pilot to take them out before they start getting too comfortable. I can offer $ \VAL V4 plus salvage rights to their gear.>
	Msg21_1 <I've heard about a big shipment of raw gundamium passing through. The convoy is well guarded, but the goods should be worth the trouble. Pay is $ \VAL V4 plus any meks that survive your assault.>
    CMsg21_1 <ifG PCRenown 75 Accept>
	Msg21_2 <One of the syndicate bigwigs set up a real nice complex just outside of town. Word is that it's full of loot, but of course it's guarded by some of his cronies. I can offer $ \VAL V4 plus salvage.>
    CMsg21_2 <ifG PCRenown 60 Accept>
	Msg21_3 <You know what's valuable? Contraband movie discs. I know where we can get our hands on a lot of them... of course we'll have to take them from the bootleggers who made them first. Pay is $ \VAL V4 plus salvage rights.>
	Msg21_4 <Some dumbass went and stole a couple of really nice mecha from the syndicate. There's a reward of $ \VAL V4 to rub them out, and if any of the meks are still functioning afterwards no-one has to be the wiser about that.>
    CMsg21_4 <ifG PCRenown 50 Accept>
	Msg21_5 <Want to hijack a nanochip shipment? The pay is $ \VAL V4 and whatever guardian meks you can salvage.>
    CMsg21_5 <ifG PCRenown 40 Accept>
	Msg21_6 <A bunch of hot cellphones are being smuggled across the border. If we get to them first, they could be ours. I can give you $ \VAL V4 plus any leftover mecha.>
	Msg21_7 <All I need you to do is take out the sentry mecha while I do my business. You don't need to know any more than that. Pay is $ \VAL V4 and salvage.>
	Msg21_8 <The plan is, we rob the ramen noodle convoy and sell the goods to my contact. It might not be lucrative but a job's a job. You have to deal with the guards. Pay is $ \VAL V4 and salvage.>
    CMsg21_8 <ifG 50 PCRenown Accept>
	Msg21_9 <I've got a small job for you. A few young punks need to be taught a lesson. I need you to eliminate their meks. It's worth $ \VAL V4 plus whatever salvage you don't absolutely destroy.>
    CMsg21_9 <ifG 30 PCRenown Accept>

	% 26 :  Combat is starting
	Msg26 <Here are the coordinates. Meet me back here after you're done.>
	Msg26_7 <>
	Msg26_8 <>
	Msg26_9 <>
	Msg26_10 <>

	% 31 :  Describe enemy point value
	Msg31 <The other side has \VAL V3 points worth of equipment. I think you can handle that.>
	Msg31_2 <>
	Msg31_3 <>
	Msg31_4 <>
	Msg31_5 <>


	% 101 :  Player is back from repairs, time to start mission.
	Msg101 <Are you ready to start the job yet?>
	% 102 :  Player doesn't have "the right stuff" for this mission.
	Msg102 <I'm busy. If you don't have anything important to say, take it elsewhere.>
    Msg102_1 <I can't talk now, I've got some business matters going on.>
	% 103 :  Memo
	Msg103 <\ELEMENT 1 in \SCENE EScene 1 offered you a criminal mission.>
	% 104 :  PC didn't want the job.
	Msg104 <You don't know what you're missing.>
	% 105 :  PC needs time to get ready.
	Msg105 <Do whatever you need to do, then come back here and we'll start.>

    Msg106 <Let me just tell you, I've got so much stuff going on. You really ought to get a mecha, then you could get a cut of the action.>

    Msg201 <You're agressive... I like that. Interested in making a bit of quick cash?>
    Msg202 <Don't knock it until you've tried it.>
    Msg203 <I don't think so. Get lost loser.>

	% 01 :  PC will accept the mission.
	Prompt1 <Tell me more.>
	Prompt1_2 <Alright, fill me in.>
	Prompt1_3 <Okay. Sounds like fun.>

	% 04 :  PC doesn't want this mission.
	Prompt4 <Sorry, I can't help you.>
	Prompt4_5 <No thanks.>
	Prompt4_6 <This isn't my kind of work.>

	% 07 :  Combat is starting.
	prompt7 <Be back when I'm finished.>
	prompt7_8 <I'm all ready to go.>

	% 09 :  How many points?
	prompt9 <What am I up against?>
	prompt9_10 <How many points am I facing?>

	% 11 :  Just a minute...
	prompt11 <I have to go get supplied first.>
	prompt11_2 <Not ready to start yet; be back soon.>

    Prompt201 <I want a piece of that action.>
    Prompt201_1 <Do you need a pilot?>
    Prompt201_2 <>

    Prompt202 <I don't even want to know.>
    Prompt202_1 <Something criminal, no doubt.>
    Prompt202_2 <>

end
